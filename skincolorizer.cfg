// SkinColorizer Lite, by @SalatielSauer
// V: 15/09/2024

setskincolorizerdir = [
	_skcl_custom_dir = $arg2
	sleep 0 [ _skcl_custom_dir = "" ] // next frame
	do [ @arg1 @arg2 ]
]

// command part <dds>texture mask param1 param2
skincolorizer = [
	if (=s $arg1 "") [skincolorizerlite] [
	arg3 = (strreplace $arg3 "<dds>" "")

	local model_path texture_path model_alias
	model_path = (format "packages/models/%1" $mdlname)
	texture_path = (format "packages/models/%1" (? (=s $_skcl_custom_dir "") $mdlname $_skcl_custom_dir))

	if (&& (!=s $_skcl_custom_dir "") (= (findfile (concatword $texture_path "/" $arg3)) 0)) [
		texture_path = $model_path
	]

	if (= (indexof $_skcl_preloaded_models $mdlname) -1) [
		append _skcl_preloaded_models_temp $mdlname
	]

	// path:packages/models/snoutx10k|part:Upper| = ...
	//echo $path:packages/models/snoutx10k|part:Upper| // [[r g b] texture]

	model_alias = (concatword "_skincolorizer_path:" $model_path "|part:" (strreplace $arg2 " " "~") "|")

	if (= (indexof $_skcl_all_model_aliases_temp $model_alias) -1) [
		//echo (concat "^f3" $_skcl_custom_dir "^f1" $model_path ":" $arg1 "|" $arg2 "|" $arg3 "|" $arg4 "|" $arg5 "|" $arg6)
		// brute force
		while [&& (!= (findfile (concatword $texture_path "/" $arg3)) 1) (!= (_strstrlast $texture_path "/") -1)] [ // cant find file & didn't reach the end yet
			texture_path = (substr $texture_path 0 (_strstrlast $texture_path "/"))
			//echo (concatword "^f4searching " $texture_path)
		]

		if (! (findfile (concatword $texture_path "/" $arg3))) [
			echo (concatword "^f7skincolorizer: ^f3could not find texture " $model_path "/^f2" $arg3)
		] [
			//echo (concatword "^f0found texture at " $texture_path "/^f8" $arg3)
		]

		// texture [r g b] selected fullbright
		$model_alias = (format "%1 %2/%3 0 0" "[1 1 1]" $texture_path $arg3)

		append _skcl_all_model_aliases_temp $model_alias
	]

	//echo $model_alias == $$model_alias
	
	do [
		//echo (format "colors: %1 | %2 | %3" @(at $$model_alias 0))
		@arg1 $arg2 (format "<mad:%1/%2/%3>%4" @(at $$model_alias 0) $arg3) $arg4 $arg5 $arg6

		nodebug [
			(strreplace @arg1 "skin" "fullbright") $arg2 @(at $$model_alias 3)
		]

	]
]]

_skcl_getselectedmodels = [
	listfilter model_alias $_skcl_all_model_aliases_temp [at $$model_alias 2]
]

_skcl_loopmodels = [
	local action
	action = $arg1

	(? (= $arg3 1) _guilistscroll looplist) model_alias $_skcl_all_model_aliases_temp [
		local model_path
		model_path = (_getinlineattr $model_alias "path")

		action $model_path (at $$model_alias 0) (at $$model_alias 1) (at $$model_alias 2) (at $$model_alias 3) $model_alias

	] $_skcl_gui_list_height 1

	_skcl_selection_counter = (listlen (_skcl_getselectedmodels))
]

_skcl_clearmodels = [
	local cleared
	cleared = []
	looplist model_alias (_skcl_getselectedmodels) [
		$model_alias = (_replacelist $$model_alias 0 (format "[%1 %2 %3]" (divf $_skcl_input0 255) (divf $_skcl_input1 255) (divf $_skcl_input2 255))) // update colors
		$model_alias = (_replacelist $$model_alias 3 $_skcl_input_fullbright) // update fullbright

		if (= (indexof $cleared $model_alias) -1) [ // it is clean
			clearmodel (strreplace (_getinlineattr $model_alias "path") "packages/models/" "") // reload model by its path
			append cleared $model_alias
		]
	]
]

// skip the non-persistent var (_skcl_all_model_aliases_temp) set by the model loader by declaring _skcl_all_model_aliases outside the skincolorizer scope,
// otherwise it will not be written to config.cfg.
_skcl_persist_aliases = [
	nodebug [
		_skcl_all_model_aliases = $_skcl_all_model_aliases_temp
		_skcl_preloaded_models = $_skcl_preloaded_models_temp
		looplist model_alias $_skcl_all_model_aliases [
			$model_alias = $$model_alias
		]
	]
]

_skcl_search_highlight = [
	local search_term source_aliases aliases_match aliases_unmatch
	search_term = $arg1
	source_aliases = $_skcl_all_model_aliases_temp
	aliases_unmatch = (listfilter model_alias $source_aliases [= (strstr (strlower $model_alias) (strlower $search_term)) -1])
	aliases_match = (listfilter model_alias $source_aliases [!= (strstr (strlower $model_alias) (strlower $search_term)) -1])

	_skcl_all_model_aliases_temp = (concat $aliases_match $aliases_unmatch)

	_curtabmodel_alias = 0
]

// shows 'neutral' parts first
_skcl_search_highlight_noteam = [
	local source_aliases aliases_match aliases_unmatch
	aliases_unmatch = (listfilter model_alias $_skcl_all_model_aliases_temp [&& (= (strstr $model_alias "red") -1) (= (strstr $model_alias "blue") -1)])
	aliases_match = (listfilter model_alias $_skcl_all_model_aliases_temp [|| (!= (strstr $model_alias "red") -1) (!= (strstr $model_alias "blue") -1)])
	_skcl_all_model_aliases_temp = (concat $aliases_unmatch $aliases_match)
]

skincolorizerlite = [
	newgui skcl [guistayopen [
		_skcl_persist_aliases
		guistrut -2
		guititle "^f8SkinColorizer ^f7Lite"
		guistrut 1
		guistrut 40 1
		
		looplist preloaded_model $_skcl_preloaded_models [
			guimodelpreview $preloaded_model "mapmodel" [] 0.1 0
			guistrut -0.2
		]
		guistrut 0
		guibutton "^f4*" [ _skcl_gui_settings ] 0
		guistrut -2.5
		guialign -1 [
			guistrut -2
			guiplayerpreview $playermodel $_skcl_preview_team (getweapon) [] 5 0
		]

		guistrut -8
		guialign 1 [
			guialign 1 [
				guistrut 22 1
				guialign 0 [
					guiimage (format "<mad:%1/%2/%3>%4" (divf $_skcl_input0 256) (divf $_skcl_input1 256) (divf $_skcl_input2 256) $_skcl_gui_white) [
						_skcl_input0 = 255
						_skcl_input1 = 255
						_skcl_input2 = 255
						_skcl_input_fullbright = 0
						_skcl_clearmodels
					] 0.5
					guicolor (+ (+ (* (* $_skcl_input0 256) 256) (* $_skcl_input1 256)) $_skcl_input2)
				]

				loop color_channel 3 [
					local input
					input = (concatword _skcl_input $color_channel)
					guilist [
						guistrut -1
						guitext (concatword "^f" (at ["3R" "0G" "1B"] $color_channel)) 0
					]
					guistrut -1
					guislider $input 0 255 [
						_skcl_clearmodels
					]
				]

				if (!= $_skcl_input_fullbright 0) [
					guialign -1 [
						guicheckbox "" _skcl_input_fullbright 1 0 [_skcl_clearmodels]
						guitext "brightness: " 0
						guilist [
							guistrut 10 1
							guislider _skcl_input_fullbright 1 64 [
								_skcl_clearmodels
							]
						]
					]
				] [
					guicheckbox "fullbright" _skcl_input_fullbright 1 0 [
						_skcl_clearmodels
					]
				]
			]
		]

		guistrut -3.5
		guialign -1 [
			guistrut 1
			guialign 0 [
				guialign 0 [
					loop p $playermodelnum [
						guibutton "" [
							playermodel @p
							sleep 0 [
								if (= $_skcl_preview_team 0) [
									_skcl_search_highlight_noteam
								] [
									_skcl_search_highlight (at ["" "blue" "red"] $_skcl_preview_team)
								]
								_skcl_search_highlight (playermodelicon @@p)
							]
						] (format "%1%2" (playermodelicon $p) (at ["" "_blue" "_red"] $_skcl_preview_team))
						guistrut -0.2
						if (=s (playermodelicon $p) (playermodelicon)) [
							guistrut -2.2
							guilist [
								guistrut -0.1
								guitext "^f8[ ]" 0
							]
						]
					]
				]
				guialign 0 [
					looplist teamset ["^f1good 1" "^f0neutral 0" "^f3evil 2"] [
						local teamname teamid
						teamname = (unescape (at $teamset 0))
						teamid = (at $teamset 1)

						if (!= $_skcl_preview_team $teamid) [
							guibutton (format "%1" $teamname) [
								_skcl_preview_team = @teamid
								sleep 0 [
									_skcl_search_highlight (playermodelicon)
									if (= $_skcl_preview_team 0) [
										_skcl_search_highlight_noteam
									] [
										_skcl_search_highlight (at ["" "blue" "red"] $_skcl_preview_team)
									]
								]
									
							] 0
						] [
							guitext (format "^f8[%1^f8]" $teamname) 0
						]
			
						guistrut 0.5
					]
				]
			]
		]
		guistrut 0.5

		if (>= (listlen $_skcl_all_model_aliases_temp) $_skcl_gui_list_height) [
			guilist [
				guitext "^f2filter^f7: " 0
				guifield _skcl_selection_search 9 [
					_skcl_search_highlight $_skcl_selection_search
				]
				if (!= (strlen $_skcl_selection_search) 0) [
					guilist [
						guistrut -0.05
						guibutton "^f2[^f3x^f2]" [ _skcl_selection_search = "" ] 0
					]
				]
			]
		] [
			guistrut 1
		]

		guialign 1 [
			//guispring 2
			guialign 0 [
				_guilistscroll model_alias $_skcl_all_model_aliases_temp [
					local model_part model_is_selected model_color r g b
					model_part = (strreplace (strreplace (_getinlineattr $model_alias "part") "~" "") "*" "*all*")
					model_is_selected = (at $$model_alias 2)
					model_color = (at $$model_alias 0)
					r = (*f (maxf (at $model_color 0) 0.1) (? $model_is_selected 255 155))
					g = (*f (maxf (at $model_color 1) 0.1) (? $model_is_selected 255 155))
					b = (*f (maxf (at $model_color 2) 0.1) (? $model_is_selected 255 155))
					guitextbox (concatword (? $model_is_selected "^f7:^f8-  ^f~" "^f7: ^f~") $model_part ) 0 0 (+ (+ (* (* $r 256) 256) (* $g 256)) $b)
				] $_skcl_gui_list_height 0
			]
		]

		guistrut (max (* (listlen $_skcl_all_model_aliases_temp) -1) (* $_skcl_gui_list_height -1))

		guialign -1 [
			guialign -1 [
				_skcl_loopmodels [
					local model_path model_color model_texture model_is_selected model_fullbright model_alias model_pretty_name
					model_path = $arg1
					model_color = $arg2
					model_texture = $arg3
					model_is_selected = $arg4
					model_fullbright = $arg5
					model_alias = $arg6

					model_pretty_name = (strreplace (strreplace $model_path "~" " ") "packages/models" "..")
					model_pretty_name = (strreplace $model_pretty_name $_skcl_selection_search (format "^f2%1" $_skcl_selection_search))
					local parent_folder

					parent_folder = (substr $model_pretty_name 0 (_strstrlast $model_pretty_name "/"))
					model_pretty_name = (? (>= (strlen $model_pretty_name) 22) (concatword (substr $parent_folder 0 (+ (_strstrlast $parent_folder "/") (- 35 (strlen $model_pretty_name)))) ".." (substr $model_pretty_name (_strstrlast $model_pretty_name "/"))  ) $model_pretty_name)

					guilist [
						guiimage (format "<thumbnail:32><mad:%1/%2/%3>%4" @model_color $model_texture) [
							clearmodel (strreplace @model_path "packages/models/" "")
						] 0.5 0 (format "<mad:%1/%2/%3>%4" @model_color $_skcl_gui_white)

						guistrut 0.5

						guibutton (format "%1%3{%2%3}" (? (!= (strstr $model_pretty_name "blue") -1) "^f1|" (? (!= (strstr $model_pretty_name "red") -1) "^f3|")) $model_pretty_name (? $model_is_selected "^f8" "^f4")) [
							// update selection/fullbright from stored values
							@model_alias = (_replacelist $@model_alias 2 (! @model_is_selected))
							_skcl_input_fullbright = @model_fullbright

							if (! @model_is_selected) [
								local model_color
								model_color = [@@@model_color]
								loop color_channel 3 [
									local color
									color = (*f (at $model_color $color_channel) 255)
									do [
										_skcl_input@color_channel = @color
									]
								]
							]
						] 0
					]
						
					guistrut -1
					guialign -1 [
						if $model_is_selected [
							guistrut 0.09
							guilist [
								guistrut -0.1
								guitext "^f8[^f3-^f8]" 0
								guistrut 0.1
							]
						] [
							guistrut 0.3
							guilist [
								guistrut -0.1
								guitext "^f0+" 0
								guistrut 0.1
							]
						]
					]
				] 0 1
				guilist [
					if (!= $_skcl_selection_counter 0) [
						guibutton (format "^f3[-]unselect (%1)" $_skcl_selection_counter) [
							_skcl_selection_last = ""
							_skcl_loopmodels [
								local model_alias
								model_alias = $arg6
								$model_alias = (_replacelist $$model_alias 2 0)
							]
						] 0
						guistrut 1
					]

					if (= (listlen $_skcl_all_model_aliases_temp) 0) [
						guilist [
							guitextbox "There are no parts loaded, make sure ^f8SkinColorizer ^f~is installed correctly and restart the client.^nParts can be loaded by adding the ^f5skincolorizer ^f~prefix before the skin command in the model configuration file:" 45 0 0xbbffff
							guitextbox "^f~...^nmd5load ^"snoutx10k.md5mesh^";^n^f0skincolorizer ^f~md5skin Body ^"upper.png^";^n^f~..." 45 0 0x999999
						]
					] [
						local select_term
						select_term = (? (=s $_skcl_selection_search "") (playermodelicon) $_skcl_selection_search)
						if (!=s $_skcl_selection_last $select_term) [
							guibutton (concat "^f0[+]select" @select_term) [
								_skcl_loopmodels [
									local model_alias
									model_alias = $arg6
									$model_alias = (_replacelist $$model_alias 2 (|| $arg4 (!= (strstr $model_alias @@select_term) -1)))
								]
								if (!= $_skcl_selection_counter 0) [
									_skcl_selection_last = @@@select_term
								]
							] 0
						]
					]
				]
			]
		]
	]] 0

	showgui skcl
]

if (=s (getalias _skcl_all_model_aliases) "") [
	_skcl_input0 = 255
	_skcl_input1 = 255
	_skcl_input2 = 255
	_skcl_input_fullbright = 0
	_skcl_all_model_aliases = []
	_skcl_all_model_aliases_temp = []
	_skcl_preloaded_models = []
	_skcl_preloaded_models_temp = []
	_skcl_reset_models_onrestart = 2
	_skcl_gui_list_height = 5
] [
	if (listlen $_skcl_all_model_aliases) [
		// keep all parts
		if (= $_skcl_reset_models_onrestart 0) [
			_skcl_all_model_aliases_temp = $_skcl_all_model_aliases
			_skcl_preloaded_models_temp = $_skcl_preloaded_models
		]

		// remove all parts
		if (= $_skcl_reset_models_onrestart 1) [
			_skcl_all_model_aliases_temp = []
			_skcl_preloaded_models_temp = []
		]

		// keep modified parts
		if (= $_skcl_reset_models_onrestart 2) [
			_skcl_all_model_aliases_temp = (listfilter model_alias $_skcl_all_model_aliases [|| (!=s (strreplace (at $$model_alias 0) ".0" "") "1 1 1") (!= (at $$model_alias 2) 0)])
			_skcl_preloaded_models_temp = (looplistconcat model_alias $_skcl_all_model_aliases_temp [strreplace (_getinlineattr $model_alias "path") "packages/models/" ""])
		]
		local current previous
		current = (listlen $_skcl_all_model_aliases_temp)
		previous = (listlen $_skcl_all_model_aliases)
		if (!= $current 0) [
			echo (format "^f7SkinColorizer Lite: ^f0%1 ^f7parts%2loaded successfully" $current (? (> $previous $current) (concatword " ^f7(^f3-" (- $previous $current) "^f7) ") " "))
		]
	]
]


_skcl_gui_white = "packages/skyboxes/white_dn.jpg"
_skcl_custom_dir = ""
_skcl_preview_team = 0
_skcl_selection_counter = 0
_skcl_selection_last = ""
_skcl_selection_search = ""

_skcl_gui_settings = [
	newgui _skcl_settings [guistayopen [
		guistrut 40 1
		guistrut -2
		guialign 0 [
			guitext "^f8SkinColorizer ^f7Lite" 0
		]
		guistrut -2
		guialign 0 [guitext "^f2[settings]" 0]
		guistrut 2
		guilist [
			guitext "^f4*" 0; guistrut 1;
			guilist [
				guistrut -0.2;
				guibutton (format "^f4on restart = [%1^f4]" (unescape (at ["^f6[0]all parts are kept" "^f3[1]all parts are removed" "^f0[2]only modified parts are kept"] $_skcl_reset_models_onrestart))) [
						_skcl_reset_models_onrestart = (? (= $_skcl_reset_models_onrestart 2) 0 (+ $_skcl_reset_models_onrestart 1))
					] 0
				]
			]
		guilist [ guitext "^f4*" 0; guistrut 1; guilist [ guistrut -0.2; guilist [ guitext "^f4parts list length =" 0; guistrut 1; guifield _skcl_gui_list_height 5 [_skcl_gui_list_height = (max $_skcl_gui_list_height 1); _curtabmodel_alias = 0] ] ] ]
		guibutton "^f7<^f8back" [skincolorizerlite] 0

	]] 0
	showgui _skcl_settings
]

_getinlineattr = [arg2 = (concatword $arg2 ":"); arg2 = (substr $arg1 (+ (strstr $arg1 $arg2) (strlen $arg2))); substr $arg2 0 (strstr $arg2 "|")]
_replacelist = [concat (sublist $arg1 0 $arg2) $arg3 (sublist $arg1 (+ $arg2 1))]
_strstrlast = [local _stra; _stra = -1; loop s (strlen $arg1) [if (=s (substr $arg1 $s 1) $arg2) [_stra = $s]]; result $_stra]
_guilistscroll = [local _curlist _cursublist; if (!=s $arg2 "") [nodebug [_curlist = $arg2; if (|| (> $(concatword "_curtab" $arg1) (listlen $_curlist)) (=s (getalias (concatword "_curtab" $arg1)) "")) [(concatword "_curtab" $arg1) = 0]; _cursublist = (sublist $_curlist $(concatword "_curtab" $arg1) $arg4); guilist [if (&& (|| (=s $arg5 "") (!= $arg5 0)) (> (listlen $_curlist) $arg4)) [guislider (concatword "_curtab" $arg1) (- (listlen $_curlist) $arg4) 0]; guilist [looplist $arg1 $_cursublist [arg3]; if (> (listlen $_curlist) $arg4) [loop _e (- $arg4 (listlen $_cursublist)) [guitext "" 0]]]]; arg6]] [arg7]]