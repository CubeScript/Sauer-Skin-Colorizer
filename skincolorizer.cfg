// Skin Colorizer by Salatiel (salatielsauer.github.io)
// V 03/02/2021

guiitemmove = [guialign $arg1 [guistrut @arg2; guilist [guistrut @arg3; @arg4]]]
rndlist = [at $arg1 (rnd (listlen $arg1))]
listadd = [result (concatword $arg1 " ^"" $arg2 "^"" )]
listreplace = [result (listsplice $arg1 $arg3 $arg2 1)]

listoccount = [
	_listoc = 0
	looplist i $arg2 [
		if (=s $arg1 $i)[_listoc = (+ $_listoc 1)]
	]
	result $_listoc
]

_sc_resetcolors = [
	_sccolors = []
	_scbskins = []
	loop c 9 [
		c = (+ $c 1)
		_sccolors = (listadd $_sccolors $arg1)
		_scbskins = (listadd $_scbskins $arg2)
	]
]
_sc_resetcolors "1 0.5 0.2" "white"

// _sc_allcolors = [loop i 8 [i = (+ @i 1); echo (concatword "^f7" (at $_scids @i) ":^t^f3" (_sc_getpartcolor @i 0 1) " ^f0" (_sc_getpartcolor @i 1 1) " ^f1" (_sc_getpartcolor @i 2 1))]]

skincolorizer = [showgui skincolorizer]
_sc_getpartcolor = [
	_scc = (at (at $_sccolors $arg1) $arg2)
	if (= $arg3 1)[
		result (substr (*f $_scc 255) 0 (strstr (*f $_scc 255) "."))
	] [
		result $_scc
	]
]

_sc_setpartcolor = [
	_sccolors = (listreplace $_sccolors $_sccurpart (concatword "^"" $arg1 "^""))
]
_sc_setbskins = [
	_scbskins = (listreplace $_scbskins $_sccurpart (concatword "^"" $arg1 "^""))
]

_sc_setdrop = [
	if (> $_sccurdrop 0) [
		_sccolors = (listreplace $_sccolors $_sccurdrop (concatword "^"" (at $_sccolors $_sccurpart) "^""))
		_scbskins = (listreplace $_scbskins $_sccurdrop (concatword "^"" (at $_scbskins $_sccurpart) "^""))
		_sccurpart = $_sccurdrop
		_sccurdrop = 0
	]
]

_sc_teamcheck = [
	if (&& (= $_sccolorR 255) (= $_sccolorG 255) (= $_sccolorB 255) (= (strstr (substr $_scbskins 8 (strlen $_scbskins)) "white") -1) (= (listoccount (at $_scbskins $_sccurpart) $_scbskins) (listlen $_scbskins)) (= (listoccount (at $_sccolors $_sccurpart) $_sccolors) (listlen $_sccolors))) [
		_sc_resetcolors "1 1 1" (at ["blue" "green" "red"] $_scskinteam)
	]
]

_sccurdrop = 0
_scskinteam = 1
_sccurpart = 1

if (=s (getalias _sccolorR) "")[_sccolorR = 0; _sccolorG = 0; _sccolorB = 0]

_scids = [null lower upper belt helmet arms gloves shoulders visor]
_sc_getid = [
	if (= $arg1 $_sccurdrop)[
		result (concatword "^f4" (at $_scids $arg1))
	] [
		if (= $arg1 $_sccurpart)[
			result (concatword "^f6" (at $_scids $arg1))
		] [
			if (!= $_sccurdrop 0)[
				result (concatword "^f0" (at $_scids $arg1))
			] [
				result (concatword "^f1" (at $_scids $arg1))
			]
		]
	]
]
_sc_refresh = [
	looplist color [R G B] [(concatword "_sccolor" $color) = (*f (_sc_getpartcolor $arg1 (indexof [R G B] $color)) 255)]
	_sccurpart = $arg1
	_sc_setdrop
]
_sc_refresh 1

newgui skincolorizer [
	guistayopen [
		guiitemmove 0 0 10 [
			//Preview shadow
			guiitemmove 0 0 -10 [
				guiimage "gui/snoutx10k/background.png" [] 5 0
			]
			//Preview
			loop i 8 [
				i = (+ $i 1)
				//echo $i ":" (_sc_getpartcolor $i 0) (_sc_getpartcolor $i 1) (_sc_getpartcolor $i 2)
				guiitemmove 0 0 -10 [
					guiimage (result (format "<mad: %1/%2/%3>gui/snoutx10k/preview_%4/%5.png" (_sc_getpartcolor $i 0) (_sc_getpartcolor $i 1) (_sc_getpartcolor $i 2) (at $_scbskins $i) $i)) [] 5 0
				]
			]
		]
		//Texture picker
		guiitemmove -1 -3.2 -5 [guiimage "gui/button_v.png" [] 2 0]
		guiitemmove -1 0 -4.6 [
			looplist c ["blue" "green" "red" "white"] [
				guiimage (format "<mad:%1>gui/white.png" (at ["0/0.4/1" "0/0.6/0" "1/0/0" @(strreplace (at $_sccolors $_sccurpart) " " "/")] (indexof ["blue" "green" "red" "white"] $c))) [_sc_setbskins @c] 0.4 0
				if (=s (at $_scbskins $_sccurpart) $c)[guiitemmove -1 -1 -1 [guitext "^f6>" 0]]
			]
			//Color dropper/filler
			guiitemmove -1 -0.3 0.5 [guiimage "gui/dropper.png" [if (= $_sccurdrop 0)[_sccurdrop = $_sccurpart] [_sccurdrop = 0]] 0.5 0]
			guiitemmove -1 -0.3 -6.2 [guiimage "gui/filler.png" [_sc_resetcolors (at $_sccolors $_sccurpart) (at $_scbskins $_sccurpart)] 0.5 0]
		]

		//Labels
		guiitemmove 0 0 -10.5 [guitext (concatword "^f7" (getname)) 0]
		guiitemmove 0 9 -9 [guibutton (concatword "^f4-^f1--" (_sc_getid 4)) [_sc_refresh 4] 0]
		guiitemmove 0 15 -8 [guibutton (concatword "^f4-^f1-" (_sc_getid 7)) [_sc_refresh 7] 0]
		guiitemmove 0 -8 -8.5 [guibutton (concatword (_sc_getid 8) "^f1--^f4-") [_sc_refresh 8] 0]
		guiitemmove -1 1.5 -7.2 [guibutton (concatword (_sc_getid 5) "^f1---^f4-") [_sc_refresh 5] 0]
		guiitemmove 0 19 -6.5 [guibutton (concatword "^f4-^f1-" (_sc_getid 6)) [_sc_refresh 6] 0]
		guiitemmove -1 5 -6 [guibutton (concatword (_sc_getid 2) "^f1-^f4-") [_sc_refresh 2] 0]
		guiitemmove 0 7 -5.2 [guibutton (concatword "^f4-^f1-" (_sc_getid 3)) [_sc_refresh 3] 0]
		guiitemmove -1 4 -2.5 [guibutton (concatword (_sc_getid 1) "^f1-^f4-") [_sc_refresh 1] 0]

		//Team selector
		guialign 0 [
			guiradio "^f1good " _scskinteam 0 [_sc_teamcheck]
			guiradio "^f0neutral " _scskinteam 1 [_sc_teamcheck]
			guiradio "^f3evil" _scskinteam 2 [_sc_teamcheck]
		]

		//Color sliders
		looplist color [R G B] [
			guislider (concatword "_sccolor" $color) 0 255 [_sc_setpartcolor (concat (divf $_sccolorR 255) (divf $_sccolorG 255) (divf $_sccolorB 255))]
			guiitemmove -1 -1 -1 [guitext (at ["3R" "0G" "1B"] (indexof [R G B] $color)) 0]
		]

		//Save button
		guiitemmove 1 0 -8 [
			guiimage "gui/button.png" [showgui "_scskinsave"] 2 0
			guiitemmove 0 0 -2.6 [guititle "^f7save"]
		]
	]
] 0

newgui _scskinsave [
	guitext "original files are kept intact"
	guitext "restart may be necessary"
	guibar
	guitext (format "apply changes to %1 ^f9skin?" (at ["1good" "0neutral" "3evil"] $_scskinteam))
	guibutton "yes" [
		textfocus _scmd5cfg
		if (=s $textshow "")[
			echo "^f3Could not save it, try again"
		] [
			echo (format "^f7Skin ^"%1^" saved successfully" (at ["good" "neutral" "evil"] $_scskinteam))
			textsave (format "packages/models/snoutx10k/%1md5.cfg" (at ["blue/" "" "red/"] $_scskinteam))
			clearmodel (concatword "snoutx10k" (at "/blue" "" "/red" $_scskinteam))
			showgui skincolorizer
		]
	]
	guibutton "no" [showgui skincolorizer] arrow_bw
	_scmd5cfg = (concatword "//delete this file if you want to revert the changes to the original ^"" (at ["good" "neutral" "evil"] $_scskinteam) "^" skin^n^n_skinparts = [" $_scids "]^n_skincolors = [" $_sccolors "]^n_skinbases = [" $_scbskins "]^nmd5dir snoutx10k^nmd5load ^"snoutx10k_sc.md5mesh^" snoutx10k^nexec ^"packages/models/mrfixit/ragdoll.cfg^"^nmd5tag Weapon tag_weapon^nlooplist p $_skinparts [^n^t_sccur = (at $_skincolors (indexof $_skinparts $p))^n^tmd5skin $p (format ^"<mad:%1/%2/%3>snoutx10k_%4.png^" (at $_sccur 0) (at $_sccur 1) (at $_sccur 2) (at $_skinbases (indexof $_skinparts $p))) ^"snoutx10k_mask.png^" .4 .1^n^tmd5bumpmap (at $_skinparts $p) ^"snoutx10k_normals.jpg^"^n]^nexec ^"packages/models/mrfixit/anims.cfg^"^nmdlscale 800^nmdlspec 175^nmdlenvmap 0 0 skyboxes/morning^n^n//generated with Skin Colorizer, by Salatiel#5274")
	guiitemmove -1 -500 0 [guifield _scmd5cfg 1]
] 0
